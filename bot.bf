Endless loop
+[-,
    If char = '!'
    >+++[-<----------->]<
    >[-]>[-]
    <<[>+>+<<-]>[<+>-]
    >[
        Get next character for comparison and preserve
        <<,[>>>+>+<<<<-]>>>>[<<<<+>>>>-]<<<<

        If char = 'v'
        >++++++++++[-<---------->]<--------
        >[-]>[-]
        <<[>+>+<<-]>[<+>-]+
        >[<->[-]]
        <[
            Print 'Hi!'
            <[-]>[-]+++++++[-<++++++++++>]<++.
            [-]>[-]++++++++++[-<++++++++++>]<+++++.
            [-]>[-]+++[-<+++++++++++>]<.
        >-]<

        Else if char = 'i'
        [-]>>>>[->+<<<<<+>>>>]>[-<+>]<<<<<
        >++++++++++[-<---------->]<---
        >[-]>[-]
        <<[>+>+<<-]>[<+>-]+
        >[<->[-]]
        <[
            Increment a cell

            Calculate the cell number
            <,,>[-]++++++[-<-------->]<

            Move to the cell
            [->>>>>>>>>>>>>>>>>>>>+<<<<<<<<<<<<<<<<<<<<]>>>>>>>>>>>>>>>>>>>>
            >++[-[+>]+[<]>-]>[[-]>]
            <<<<<<<<<<+>>>>>>>>>>
            <<<<<<<<<<<<<<<<<<<<
        >-]<

        Else if char = 'd'
        [-]>>>>[->+<<<<<+>>>>]>[-<+>]<<<<<
        >++++++++++[-<---------->]<
        >[-]>[-]
        <<[>+>+<<-]>[<+>-]+
        >[<->[-]]
        <[
            Increment a cell

            Calculate the cell number
            <,,>[-]++++++[-<-------->]<

            Move to the cell
            [->>>>>>>>>>>>>>>>>>>>+<<<<<<<<<<<<<<<<<<<<]>>>>>>>>>>>>>>>>>>>>
            >++[-[+>]+[<]>-]>[[-]>]
            <<<<<<<<<<->>>>>>>>>>
            <<<<<<<<<<<<<<<<<<<<
        >-]<
    >>[-]]
]